sp {all-states*elaborate*linkstate
   (state <s> ^superstate <ts>)
   (<ts> ^linkstate <ss>)
-->
   (<s> ^linkstate <ss>)
}

sp {resolve-conditions*elaborate*type
   (state <s> ^name resolve-conditions)
-->
   (<s> ^type action goal failure)
}

#learn conditions of predicates if unchunked
sp {resolve-conditions*elaborate*nlp-set
   (state <s> ^name resolve-conditions
             ^linkstate <ts>)
   (<ts> ^{<type> << action goal predicate failure heuristic >>}.condition <c>)# ^gtype <type>)
-->
   (<s> ^condition <c>)
}

sp {resolve-conditions*propose*retrieve-predicate-structure
   (state <s> ^name learn-predicate)
   (<s> ^gtype <gtype>
       -^predicate.link <co>
         ^<gtype> <g>)
   (<g> ^condition <co>)
   (<co> ^type concept ^attribute predicate ^name <name>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name chunk-predicate-structure
        ^<gtype>.condition <co>
        ^gtype <gtype>)
}

#try to evaluate conditions whose arguments have been determined
sp {resolve-conditions*propose*learn-condition
   (state <s> ^name resolve-conditions
              ^condition <co>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name learn-condition
        ^condition <co>)
}

sp {resolve-conditions*prefer*retrieve-predicate-structure*overlearn-condition
   (state <s> ^name resolve-conditions
              ^operator <o2> +
              ^operator <o> +)
   (<o> ^name learn-condition)
   (<o2> ^name retrieve-predicate-structure)
-->
   (<s> ^operator <o2> > <o>)
}

sp {resolve-conditions*disprefer*concepts
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^type concept)
-->
   (<s> ^operator <o> <)
}

sp {resolve-conditions*disprefer*input-args
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^attribute input-arg)
-->
   (<s> ^operator <o> <)
}

sp {resolve-conditions*prefer*learn-condition*childresults
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^args.<< 1 2 3 4 5 6 7 8 9 10 >> <c1>)
   (<c1> -^result.set.instance.{1} ^return-type number)#JK think this is right... add rtype
-->
   (<s> ^operator <o> <)
}

sp {resolve-conditions*prefer*learn-condition*childresults-under
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^args.<< 1 2 3 4 5 6 7 8 9 10 >> <c1>)
   (<c1> -^result.set.instance.{1}.<something> -^return-type number)
-->
   (<s> ^operator <o> <)
}

sp {resolve-conditions*prefer*learn-condition*reject-complete
   (state <s> ^name resolve-conditions
              ^linkstate <ls>
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^result.set.instance.{1} <a>)
  -{(state <ls>)
    (<a> -^<something>)}
-->
   (<s> ^operator <o> -)
}

#TODO fix number issue
sp {resolve-conditions*prefer*learn-condition*reject-complete*number
   (state <s> ^name resolve-conditions
              ^operator <o> +)
   (<o> ^name learn-condition
        ^condition <co>)
   (<co> ^result.set.instance.{1} <something> ^return-type number ^rtype single)
-->
   (<s> ^operator <o> -)
}
