sp {top-state*apply*init-agent*create*name
	(state <s> ^top-state <s>
				^operator.name init-agent)
-->
	(<s> ^name rosie)
}

### the agent maintains an interaction state
## initialized at agent initialization

sp {top-state*apply*init-agent*create*interaction-stack
   (state <s> ^top-state <s>
              ^operator.name init-agent)
-->
   (<s> ^interaction <int>)
   (<int> ^stack <stack>
          ^status <status>
          ^changes <changes>)
   (<stack> ^segment <seg>
            ^segments.segment <seg>)
   (<seg> ^id 10
   	  ^purpose none
	  ^counter 1)
}

# Create the perception-monitor used to compare perception to the world
sp {top-state*apply*init-agent*create*perception-monitor
   (state <s> ^top-state <s>
              ^agent-params.domain << magicbot tabletop fetch >>
              ^operator.name init-agent)
-->
   (<s> ^perception-monitor <mon>)
   (<mon> ^change-info <info>
          ^changes <changes>
          ^object-monitor <obj-mon>
          ^predicate-monitor <pred-mon>)
}

sp {top-state*apply*init-agent*create*perception-monitor*internal
   (state <s> ^top-state <s>
              ^agent-params.domain internal
              ^operator.name init-agent)
-->
   (<s> ^perception-monitor <mon>)
   (<mon> ^change-info <info>
          ^changes <changes>)
}
