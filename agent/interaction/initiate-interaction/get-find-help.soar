# For an cant-find-object, where multiple objects match..
# Will ask a which question to get more information

sp {interpret-failure*propose*interpret-cant-find-object
   (state <s> ^name interpret-failure
              ^failure <failure>
             -^handled <failure>)
   (<failure> ^type cant-find-object
             ^object <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-cant-find-object
        ^object <obj>)
}

sp {interpret-failure*apply*interpret-cant-find-object*mark*handled
   (state <s> ^name interpret-failure
              ^operator.name interpret-cant-find-object
              ^failure <failure>)
-->
   (<s> ^handled <failure>)
}

sp {interpret-failure*apply*interpret-cant-find-object*create*new-segment
   (state <s> ^name interpret-failure
              ^operator <o>)
   (<o> ^name interpret-cant-find-object
        ^object <obj>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
        ^originator agent)
   (<p> ^type get-find-help
        ^satisfaction.dialog-event <de>)
   (<de> ^type ANY
         ^originator instructor)
}

sp {interpret-failure*apply*interpret-cant-find-object*create*message
   (state <s> ^name interpret-failure
              ^operator <o>)
   (<o> ^name interpret-cant-find-object
        ^object <obj>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type cant-find-object
          ^object <obj>)
}


   