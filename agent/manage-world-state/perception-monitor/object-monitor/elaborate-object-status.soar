# object-monitor.object-info.status <status>
#   Contains information about the status of the object (visible/in-view/etc)

# Elaborate the status identifier
sp {top-state*perception-monitor*object-monitor*object-info*elaborate*status
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.{ << object-info new-object-info >> } <obj-info>)
-->
   (<obj-info> ^status <status>)
}

##################################################################
#
# visible << true false >>
#   true if the belief-obj has a perception-obj
#
##################################################################

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*visible*true
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^perception-obj <perc-obj>
               ^status <status>)
-->
   (<status> ^visible true)
}

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*visible*true*current-location
   (state <s> ^superstate nil
           ^io.input-link.self.current-waypoint.waypoint-handle <wp-handle>
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^status <status>
            ^wm-obj.waypoint <wp-handle>)
-->
   (<status> ^visible true)
}

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*visible*false
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^status <status>)
   (<status> -^visible true)
-->
   (<status> ^visible false)
}

##################################################################
#
# Monitoring occluded objects
#   is-occluded << true false >> - based on the last time occlusion was checked
#   occlusion-timer-expired true - if check-occlusion-time is passed
#
##################################################################

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*is-occluded*false
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info.status <status>)
   (<status> -^is-occluded true)
-->
   (<status> ^is-occluded false)
}

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*occlusion-timer-expired*true
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^io.input-link.time.seconds <t>)
   (<obj-info> ^status <status>
               ^check-occlusion-time <= <t>)
   (<status> ^is-occluded true)
-->
   (<status> ^occlusion-timer-expired true)
}

###################################################
#
# stability-timer-expired true
#   created when the check-stability-time is passed
#
###################################################

sp {top-state*perception-monitor*object-monitor*new-object-info*stability-timer-expired
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.new-object-info <obj-info>
              ^io.input-link.time.seconds <t>)
   (<obj-info> ^status <status>
               ^check-stability-time <= <t>)
-->
   (<status> ^stability-timer-expired true)
}

###################################################
#
# growth-timer-expired true
#   created when the check-growth-time is passed
#
###################################################

sp {top-state*perception-monitor*object-monitor*object-info*growth-timer-expired
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^io.input-link.time.seconds <t>)
   (<obj-info> ^status <status>
               ^check-growth-time <= <t>)
-->
   (<status> ^growth-timer-expired true)
}

##################################################################
#
# in-view << true false >>
#   true if the belief object is in the robot's view volume
#
#################################################################

# Objects are always in view in the tabletop domain
sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*in-view*true*tabletop*domain
   (state <s> ^superstate nil
              ^agent-params.domain << tabletop fetch >>
              ^perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^status <status>)
-->
   (<status> ^in-view true)
}

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*in-view*true
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^perception-monitor.robot-view-filter.result.record <r>)
   (<obj-info> ^belief-obj.id <bel-id>
               ^status <status>)
   (<r> ^params.b <bel-id>
        ^value true)
-->
   (<status> ^in-view true)
}

sp {top-state*perception-monitor*object-monitor*object-info*status*elaborate*in-view*false
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^perception-monitor.robot-view-filter.result.record <r>)
   (<obj-info> ^belief-obj.id <bel-id>
               ^status <status>)
   (<r> ^params.b <bel-id>
        ^value false)
-->
   (<status> ^in-view false)
}

