sp {action-space*execute*propose*complete-action*success*desired*satisfied
   (state <s> ^problem-space.action-space execute
              ^desired.satisfied true)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-action
        ^status success)
}

sp {action-space*execute*propose*complete-action*unrecoverable-failure
   (state <s> ^problem-space.action-space execute
              ^current-action.unrecoverable-failure <name>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name complete-action
        ^status failure
        ^failure-name <name>)
}

sp {action-space*execute*apply*complete-action*mark*status*success
   (state <s> ^problem-space.action-space execute
              ^operator <o>
              ^current-action <a>)
   (<o> ^name complete-action
        ^status <status>)
-->
   (<a> ^status <status>)
}

sp {action-space*execute*apply*complete-action*remove*output*commands
   (state <s> ^problem-space.action-space execute
              ^operator.name complete-action
              ^current-action <a>
              ^io.output-link <out>)
   (<a> ^sent-command <cmd>)
   (<out> ^<out-name> <cmd>)
-->
   (<out> ^<out-name> <cmd> -)
}
