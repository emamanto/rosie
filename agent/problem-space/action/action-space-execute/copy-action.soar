# When there is a new action but no corresponding segment, create a new segment on the action stack,
# using copy-action to create the operator to be pushed

# use copy-action (in action-utils) to copy the action-operator
sp {action-space*execute*no*action*segment*propose*copy-action
   (state <s> ^problem-space.action-space execute
              ^action-operator <action>
             -^current-action)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-action
        ^source-action <action>
        ^destination <dest>
        ^copy-name execution-operator)
}

# create the new action segment
sp {action-space*execute*apply*copy-action*create*new-action-segment
   (state <s> ^problem-space.action-space execute
              ^operator <o>)
   (<o> ^name copy-action
        ^source-action.action-handle <h>
        ^destination <dest>)
-->
   (<s> ^new-action-segment <dest>)
   (<dest> ^action-handle <h>
           ^start-of-execution true)
}

# push the segment onto the action stack
sp {action-space*execute*apply*copy-action*push*new-action-segment*on*action-stack
   (state <s> ^problem-space.action-space execute
              ^operator.name copy-action
              ^new-action-segment <seg>
              ^top-state.action-stack.changes <chgs>)
   (<seg> ^execution-operator <op>)
-->
   (<chgs> ^push <seg>)
}