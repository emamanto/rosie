sp {op_find1*categorize*ltm
   (state <s> ^name op_find1
              ^operator <o> +
             -^child-action.execution-operator <o>)
   (<o> ^name << op_go-to-location1 op_face1 >> )
-->
   (<o> ^category stm)
}

sp {op_find1*categorize*ltm
   (state <s> ^name op_find1
              ^operator <o> +
             -^child-action.execution-operator <o>)
   (<o> ^name << op_scan1 op_recall1 op_think1 >> )
-->
   (<o> ^category ltm)
}

sp {op_find1*categorize*interaction
   (state <s> ^name op_find1
              ^operator <o> +
             -^child-action.execution-operator <o>)
   (<o> ^name << ask-for-help >> )
-->
   (<o> ^category interaction)
}

sp {op_find1*categorize*exploration
   (state <s> ^name op_find1
              ^operator <o> +
             -^child-action.execution-operator <o>)
   (<o> ^name << op_explore1 >> )
-->
   (<o> ^category exploration)
}

sp {op_find1*prefer*think*recall*over*scan
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
  (<o1> ^name << op_think1 op_recall1 >>)
   (<o2> ^name op_scan1)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*scan*over*explore
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name op_scan1
         ^category exploration)
   (<o2> ^name op_explore1
         ^category exploration)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*learn-task-subaction*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name learn-task-subaction)
   (<o2> ^category << stm ltm interaction exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*equal*preference*same*category
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
  (<o1> ^category <cat>)
   (<o2> ^category <cat>)
-->
   (<s> ^operator <o1> = <o2>)
}

sp {op_find1*prefer*stm*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category stm)
   (<o2> ^category << ltm interaction exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*prefer*ltm*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category ltm)
   (<o2> ^category << interaction exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}
sp {op_find1*prefer*interaction*over*others
   (state <s> ^name op_find1
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^category interaction)
   (<o2> ^category << exploration >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {op_find1*reject*subaction*if*not*matched
   (state <s> ^name op_find1
              ^operator <o> +
              ^action-matched false)
   (<o> ^category <any>)
-->
   (<s> ^operator <o> -)
}

sp {op_find1*reject*ask-for-help*if*child-action
   (state <s> ^name op_find1
              ^operator <o> +
              ^child-action <a>)
   (<o> ^name ask-for-help)
-->
   (<s> ^operator <o> -)
}

sp {op_find1*reject*report-success*if*child-action
   (state <s> ^name op_find1
              ^operator <o> +
              ^child-action <a>)
   (<o> ^name report-success)
-->
   (<s> ^operator <o> -)
}

sp {op_find1*reject*op_scan*if*scanned
   (state <s> ^name op_find1
              ^operator <o> +
              ^world.robot.current-location.handle <handle>
              ^current-action.scanned <handle>
              -^child-action.execution-operator <o>)
   (<o> ^name op_scan1)
-->
   (<s> ^operator <o> -)
}

sp {op_find1*apply*op_scan*mark*scanned
   (state <s> ^name op_find1
              ^operator.name op_scan1
              ^child-action.status << success failure >>
              ^world.robot.current-location.handle <handle>
              ^current-action <a>)
-->
   (<a> ^scanned <handle>)
}

sp {op_find1*apply*op_explore*mark*explored
   (state <s> ^name op_find1
              ^operator.name op_explore1
              ^child-action.status << success failure >>
              ^current-action <a>)
-->
   (<a> ^explored true)
}

sp {op_find1*reject*op_explore*if*explored
   (state <s> ^name op_find1
              ^operator <o> +
              ^current-action.explored true
              -^child-action.execution-operator <o>)
   (<o> ^name op_explore1)
-->
   (<s> ^operator <o> -)
}
