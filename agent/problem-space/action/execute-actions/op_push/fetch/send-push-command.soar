sp {op_push1*fetch*propose*send-push-command
   (state <s> ^name op_push1
              ^problem-space.action.type execute
              ^target-object <obj>
              ^projection-result <res>
              ^world.robot.arm.moving-status wait
             -^current-action.push-command)
   (<obj> ^predicates.visible true)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-push-command
        ^object <obj>
        ^destination <res>)
}

sp {op_push1*fetch*apply*send-push-command*send
   (state <s> ^name op_push1
              ^operator <o>
              ^io.output-link <ol>
              ^current-action <a>)
   (<o> ^name send-push-command
		^object.handle <obj-handle>
  		^destination <dest>)
-->
   (<ol> ^push <cmd>)
   (<cmd> ^object-handle <obj-handle>
          ^location <dest>)
   (<a> ^push-command <cmd>)
}

sp {op_push1*fetch*apply*send-push-command*mark*object*moving
   (state <s> ^name op_push1
              ^operator <o>)
   (<o> ^name send-push-command
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status moving)
}

