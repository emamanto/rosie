sp {op_put-down1*internal*propose*perform-putdown
   (state <s> ^name op_put-down1
              ^problem-space.action.type execute
              ^held-object.predicates.arm-status grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-putdown)
}

sp {op_put-down1*internal*apply*perform-putdown*remove*grabbed
   (state <s> ^name op_put-down1
              ^operator.name perform-putdown
              ^held-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
}

sp {op_put-down1*internal*apply*perform-putdown*add*predicate
   (state <s> ^name op_put-down1
              ^operator.name perform-putdown
              ^held-object <obj>
              ^predicate-handle <pred-h>
              ^destination <dest>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle <pred-h>)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <dest>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <dest>)
}


sp {op_put-down1*internal*apply*perform-putdown*add*predicate*in*current-location
   (state <s> ^name op_put-down1
              ^operator.name perform-putdown
              ^held-object <obj>
              ^world.robot.current-location <loc>
              ^world.predicates.predicate <pred>)
   (<pred> ^handle in1)
   -{ (<pred> ^instance <i>)
      (<i> ^1 <obj> ^2 <loc>) }
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}
