#### report-failure-multiple-arguments ####

### composite action - if the action has multiple versions of an argument, call this
sp {execute-action*composite*propose*report-failure-multiple-arguments
   (state <s> ^name execute-action
              ^current-action.execution-operator <aop>)
   (<aop> ^<arg-name> <arg1>
          ^<arg-name> { <arg2> <> <arg1> })
   (<arg1> ^arg-type <any>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure-multiple-arguments
        ^action-operator <aop>
        ^arg-name <arg-name>)
}

### composite action - elaborate the multiple arguments onto the operator
sp {execute-action*composite*elaborate*report-failure-multiple-arguments*argument
   (state <s> ^name execute-action
              ^operator <o> +
              ^current-action.execution-operator <aop>)
   (<o> ^name report-failure-multiple-arguments
        ^arg-name <arg-name>)
   (<aop> ^<arg-name> <arg>)
-->
   (<o> ^argument <arg>)
}

#### report-failure-missing-argument ####

sp {execute-action*composite*propose*report-failure-missing-argument
   (state <s> ^name execute-action
              ^current-action.execution-operator <aop>)
   (<aop> ^missing-argument <arg-name>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure-missing-argument
        ^action-operator <aop>
        ^arg-name <arg-name>)
}
