### composite action - if the action has multiple versions of an argument, call this
sp {problem-space*action*composite*propose*report-failure-multiple-arguments
   (state <s> ^problem-space.name action
              ^action-operator <aop>)
   (<aop> ^<arg-name> <arg1>
          ^<arg-name> { <arg2> <> <arg1> })
   (<arg1> ^arg-type <any>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure-multiple-arguments
        ^action-operator <aop>
        ^arg-name <arg-name>)
}

### composite action - elaborate the multiple arguments onto the operator
sp {problem-space*action*composite*elaborate*report-failure-multiple-arguments*argument
   (state <s> ^problem-space.name action
              ^operator <o> +
              ^action-operator <aop>)
   (<o> ^name report-failure-multiple-arguments
        ^arg-name <arg-name>)
   (<aop> ^<arg-name> <arg>)
-->
   (<o> ^argument <arg>)
}

### APPLY report-failure-multiple-arguments
        
sp {problem-space*action*apply*report-failure-multiple-arguments*create*failure
   (state <s> ^problem-space.name action
              ^operator <o>)
   (<o> ^name report-failure-multiple-arguments
        ^action-operator <aop>
        ^arg-name <arg-name>)
-->
   (<s> ^failure <f>)
   (<f> ^type multiple-arguments
        ^arg-name <arg-name>
        ^action-operator <aop>
        ^argument-info <arg-info>)
   (<arg-info> ^item-type object
               ^predicates <preds>)
}

sp {problem-space*action*apply*report-failure-multiple-arguments*copy*predicates
   (state <s> ^problem-space.name action
              ^operator <o>
              ^failure.argument-info.predicates <preds>)
   (<o> ^name report-failure-multiple-arguments
        ^argument.<any> <arg1>)
   (<arg1> ^item-type object
           ^predicates.{ <prop> << color shape size category name >> } <pred>)
   -{(<o> ^argument.<any> { <arg2> <> <arg1> })
     (<arg2> -^predicates.<prop> <pred>)}
-->
   (<preds> ^<prop> <pred>)
}

sp {problem-space*action*apply*report-failure-multiple-arguments*create*copied-preds*flag
   (state <s> ^problem-space.name action
              ^operator.name report-failure-multiple-arguments
              ^failure <f>)
   (<f> ^argument-info.predicates <preds>)
-->
   (<f> ^copied-preds true)
}

sp {problem-space*action*apply*report-failure-multiple-arguments*report-failure
   (state <s> ^problem-space.name action
              ^operator.name report-failure-multiple-arguments
              ^failure <f>
              ^top-state.interaction.status <status>)
   (<f> ^copied-preds true)
-->
   (<status> ^failure <f>)
}
