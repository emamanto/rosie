# Failure: multiple-arguments
#   When an action-operator has multiple instances of an argument
# 
# (<s> ^failure <f>)
# (<f> ^type multiple-arguments
#      ^arg-name <arg-name>      # Name of the duplicated argument
#      ^action-operator <aop>    # The action with the duplicate argument
#      ^argument-info <arg-info> # description of the duplicate argument object
#  
# Effects:
#   Push Segment: clarify-argument
#   Send Message: multiple-arguments

sp {interpret-failure*propose*interpret-multiple-arguments
   (state <s> ^name interpret-failure
              ^failure <f>
             -^handled <f>)
   (<f> ^type multiple-arguments
        ^argument-info <arg-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name interpret-multiple-arguments
        ^argument-info <arg-info>)
}

sp {interpret-failure*apply*interpret-multiple-arguments*mark*handled
   (state <s> ^name interpret-failure
              ^operator.name interpret-multiple-arguments
              ^failure <f>)
-->
   (<s> ^handled <f>)
}

sp {interpret-failure*apply*interpret-multiple-arguments*create*message
   (state <s> ^name interpret-failure
              ^operator <o>)
   (<o> ^name interpret-multiple-arguments
        ^argument-info <info>)
-->
   (<s> ^outgoing-message <msg>)
   (<msg> ^type multiple-arguments
          ^argument-info <info>)
}

sp {interpret-failure*apply*interpret-multiple-arguments*create*new-segment-info
   (state <s> ^name interpret-failure
              ^operator <o>
              ^failure <f>)
   (<f> ^action-operator <aop>
        ^arg-name <arg-name>)
   (<o> ^name interpret-multiple-arguments
        ^argument-info <info>)
-->
   (<s> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type clarify-argument
        ^parameters <params>
   	    ^satisfaction.dialog-event <de>)
   (<de> ^type object-definition
         ^originator instructor)
   (<params> ^argument-info <info>
             ^action-operator <aop>
             ^arg-name <arg-name>)
}

   