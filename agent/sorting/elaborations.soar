sp {sorting-state*elaborate*problem-space
   (state <s> ^name sort-blocks)
-->
   (<s> ^problem-space <ps>)
   (<ps> ^perception <perc>)
   (<perc> ^attend-to-all-perception yes)
}

sp {sorting-state*elaborate*distance-list
   (state <s> ^name sort-blocks)
-->
   (<s> ^distances <d>)
}

sp {sorting-state*elaborate*superoperator
   (state <s> ^name sort-blocks
              ^superstate <ss>)
   (<ss> ^operator <oss>)
   (<s> ^operator <o> +)
   (<o> ^name << finish-last-action replay-last-action >>)
-->
   (<oss> ^interruptible no)
}

sp {sorting-state*elaborate*clear-location
	(state <s> ^name sort-blocks
               ^top-state <ts>)
    (<ts> ^world.predicates <preds>)
    (<preds> ^predicate <p>)
    (<p> ^handle on1
         ^instance <i>)
    (<i> ^1 <b>
         ^2 <loc>)
    (<loc> ^handle <h>)
-->
	(<s> ^not-clear <h>)
}

sp {sorting-state*elaborate*move-block-state
   (state <s> ^name move-block
              ^superstate.operator <o>)
   (<o> ^block <b>
        ^location <loc>)
-->
   (<s> ^block <b>
        ^location <loc>)
}

sp {sorting-state*elaborate*replay-state
   (state <s> ^name replay-last-action
              ^superstate.operator <o>)
   (<o> ^action <a>
        ^command <cmd>)
-->
   (<s> ^action <a>
        ^command <cmd>)
}

sp {replay-state*elaborate*move-block-state*success
   (state <s> ^name move-block
              ^superstate <ss>)
   (<ss> ^name replay-last-action
         ^action <a>
         ^command.status success)
   (<a> ^action-type <at>
        ^block-volume <v>)
-->
   (<s> ^prev-chosen-action <at>
        ^prev-successful true
        ^block-volume <v>)
}

sp {replay-state*elaborate*move-block-state*failure
   (state <s> ^name move-block
              ^superstate <ss>)
   (<ss> ^name replay-last-action
         ^action <a>
         ^command <cmd>)
   (<cmd> ^status failure
          ^failure-reason <r>)
   (<a> ^action-type <at>
        ^block-volume <v>)
-->
   (<s> ^prev-chosen-action <at>
        ^prev-failure-reason <r>
        ^block-volume <v>)
}

sp {move-block*elaborate*volume-check
   (state <s> ^name move-block
              ^block-volume <v1>
              ^top-state.block-history <h>
              ^operator <o1> +
              ^operator <o2> +)
   (<h> ^action <a>)
   (<a> ^action-type move
	    ^failure grasping
        ^block-volume <v2> <= <v1>)
   (<o1> ^name choose-move)
   (<o2> ^name choose-push)
-->
   (<s> ^prev-failure true)
}

sp {move-block*elaborate*volume-check*success
   (state <s> ^name move-block
              ^block-volume <v1>
              ^top-state.block-history <h>
              ^operator <o1> +
              ^operator <o2> +)
   (<h> ^action <a>)
   (<a> ^action-type move
	    ^success true
        ^block-volume <v2> => <v1>)
   (<o1> ^name choose-move)
   (<o2> ^name choose-push)
-->
   (<s> ^prev-success true)
}