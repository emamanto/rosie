# Very first, replay last action for RL
sp {sort-blocks*propose*replay-last-action*move
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
              ^top-state.io.output-link <ol>
             -^replay done)
   (<h> ^last-action <a>)
   (<a> ^action-type move)
   (<ol> ^pick-up <pu>)
   (<pu> ^status << success failure >>
        -^sort-handled true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name replay-last-action
        ^action <a>
        ^command <pu>)
}

sp {sort-blocks*propose*replay-last-action*push
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
              ^top-state.io.output-link <ol>
             -^replay done)
   (<h> ^last-action <a>)
   (<a> ^action-type push)
   (<ol> ^push <p>)
   (<p> ^status << success failure >>
       -^sort-handled true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name replay-last-action
        ^action <a>
        ^command <p>)
}

# Then, update history with what happened with the last action
sp {sort-blocks*propose*finish*last-action*move
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
              ^top-state.io.output-link <ol>
              ^replay done)
   (<h> ^last-action <a>)
   (<a> ^action-type move)
   (<ol> ^pick-up <pu>)
   (<pu> ^status << success failure >>
        -^sort-handled true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name finish-last-action
        ^command <pu>)
   (write (crlf) |Handling previous move action|)
}

sp {sort-blocks*propose*finish*last-action*push
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
              ^top-state.io.output-link <ol>
              ^replay done)	
   (<h> ^last-action <a>)
   (<a> ^action-type push)
   (<ol> ^push <p>)
   (<p> ^status << success failure >>
       -^sort-handled true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name finish-last-action
        ^command <p>)
   (write (crlf) |Handling previous push action|)
}

sp {sort-blocks*apply*finish*last-action*success
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
			  ^operator <o>)
   (<h> ^last-action <a>)
   (<o> ^name finish-last-action
		^command <cmd>)
   (<cmd> ^status success)
-->
   (<h> ^action <a>
        ^last-action <a> -)
   (<a> ^success true)
   (<cmd> ^sort-handled true)
   (write (crlf) |Recorded successful action| <a>)
}

sp {sort-blocks*apply*finish*last-action*failure
   (state <s> ^name sort-blocks
              ^top-state.block-history <h>
			  ^operator <o>)
   (<h> ^last-action <a>)
   (<o> ^name finish-last-action
        ^command <cmd>)
   (<cmd> ^status failure
          ^failure-reason <r>) 
-->
   (<h> ^action <a>
        ^last-action <a> -)
   (<a> ^failure <r>)
   (<cmd> ^sort-handled true)
   (write (crlf) |Recorded failed action| <a>)
}