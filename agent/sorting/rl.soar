sp {replay-last-action*propose*move-block
	(state <s> ^name replay-last-action
               ^action <a>
               ^command <cmd>
              -^replay done)
-->
	(<s> ^operator <o> +)
    (<o> ^name move-block)
}

sp {replay-last-action*propose*finish-replay
	(state <s> ^name replay-last-action
               ^replay done)
-->
	(<s> ^operator <o> +)
    (<o> ^name finish-replay)
}

sp {replay-last-action*apply*finish-replay
	(state <s> ^name replay-last-action
               ^superstate <ss>
               ^operator.name finish-replay)
-->
	(<ss> ^replay done)
}

sp {move-block*replay*propose*finish-replay
	(state <s> ^name move-block
               ^result-learned true)
-->
    (<s> ^operator <o> + >)
    (<o> ^name move-finish-replay)
}

sp {move-block*apply*finish-replay
	(state <s> ^name move-block
               ^superstate <ss>
               ^operator.name move-finish-replay)
-->
	(<ss> ^replay done)
}

sp {move-block*replay*apply*choose-move*success
   (state <s> ^name move-block
              ^reward-link <rl>
              ^block-volume <v>
              ^operator.name choose-move
              ^prev-successful true)
-->
   (<rl> ^reward.value 1)
   (<s> ^result-learned true)
   (write |Successful move reward|)
}

sp {move-block*replay*apply*choose-push*success
   (state <s> ^name move-block
              ^reward-link <rl>
              ^block-volume <v>
              ^operator.name choose-push
              ^prev-successful true)
-->
   (<rl> ^reward.value 1)
   (<s> ^result-learned true)
   (write |Successful push reward|)
}

sp {move-block*replay*apply*choose-move*fail
   (state <s> ^name move-block
              ^reward-link <rl>
              ^block-volume <v>
              ^operator.name choose-move
              ^prev-failure-reason grasping)
-->
   (<rl> ^reward.value -1)
   (<s> ^result-learned true)
   (write |Failed move reward, grasping|)
}

sp {move-block*replay*apply*choose-move*fail*not-grasp
   (state <s> ^name move-block
              ^reward-link <rl>
              ^block-volume <v>
              ^operator.name choose-move
              ^prev-failure-reason <> grasping)
-->
   (<rl> ^reward.value -0.1)
   (<s> ^result-learned true)
   (write |Failed move reward, not grasping|)
}

sp {move-block*replay*apply*choose-push*fail
   (state <s> ^name move-block
              ^block-volume <v>
              ^reward-link <rl>
              ^operator.name choose-push
              ^prev-failure-reason <r>)
-->
   (<rl> ^reward.value -1)
   (<s> ^result-learned true)
   (write |Failed push reward|)
}

sp {move-block*select-move
	(state <s> ^name move-block
               ^block-volume { > 0.000008 <= 0.001 }
               ^operator <o> +)
    (<o> ^name choose-move)
-->
	(<s> ^operator <o> = 0.5)
 	#(write (crlf) |Select-move small|)
 }

sp {move-block*select-move*2
	(state <s> ^name move-block
               ^block-volume { > 0.001 < 0.005 }
               ^operator <o> +)
    (<o> ^name choose-move)
-->
	(<s> ^operator <o> = 0.5)
    #(write (crlf) |Select-move large|)
 }

sp {move-block*select-push
	(state <s> ^name move-block
               ^block-volume { > 0.000008 <= 0.001 }
               ^operator <o> +)
    (<o> ^name choose-push)
-->
	(<s> ^operator <o> = 0.1)
 	#(write (crlf) |Select-push small|)
 }

sp {move-block*select-push*2
	(state <s> ^name move-block
               ^block-volume { > 0.001 < 0.005 }
               ^operator <o> +)
    (<o> ^name choose-push)
-->
	(<s> ^operator <o> = 0.1)
    #(write (crlf) |Select-push large|)
 }

