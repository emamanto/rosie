sp {find-push-vector*propose*report-failure*no-predicate
   (state <s> ^name find-push-vector
             -^predicate-handle)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type no-predicate)
}

sp {find-push-vector*propose*report-failure*no-predicate*retrieved
   (state <s> ^name find-push-vector
              ^predicate failure)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type no-predicate)
}

sp {find-push-vector*propose*report-failure*no-relation
   (state <s> ^name find-push-vector
              ^predicate.relation <rel>
              ^retrieved-lti <rel>)
   (<rel> -^type)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type no-relation)
}

sp {find-push-vector*propose*report-failure*no-object
   (state <s> ^name find-push-vector
              ^argument-list.<arg-num> <arg>
              ^svs-link.spatial-scene <scene>)
   -{(<arg> ^svs-obj.id <id>)
     (<scene> ^child.id <id>)}
-->
   (<s> ^operator <o> + > =)
   (<o> ^name report-failure
        ^failure-type no-object)
}

sp {find-push-vector*apply*report-failure*copy*superstate
   (state <s> ^name find-push-vector
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name report-failure
        ^failure-type <type>)
-->
   (<ss> ^projection-result failure
         ^find-push-vector-failure <type>)
}